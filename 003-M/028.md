0xGoodess

high

# liquidate may fail due to debiting liquidation fee from 0

## Summary
liquidate may fail due to debiting liquidation fee from 0

## Vulnerability Detail
Collateral has a `liquidate` function, which operates on OptimisticLedger during liquidating a user following the below steps:

0. fetch the current totalCollateral of the user. (Important)
1.  close a user account by `product.closeAll(account)` 
2. reduce the liquidation fee from user balance on OptimisticLedger by `_products[product].debitAccount(account, fee)`

During `closeAll`, Product contract would call `_closeMake` and `_closeTake`; both of which would call `settleAccount` on the `OptimisticLedger`. `settleAccount` could handle shortfall since it treats the amount to add as `Fixed18`, and set the user balance to 0 in case shortfall happens.

However, during the subsequent fee reduction, the fee is only debited through `debitAccount`, However at this point the account balance could be 0 already after `settleAccount` is called on both `_closeTake` and `_closeMake`; this would revert the `liquidate` call.

liquidate
```solidity
    function liquidate(address account, IProduct product)
    external
    nonReentrant
    notPaused
    isProduct(product)
    settleForAccount(account, product)
    {
        if (product.isLiquidating(account)) revert CollateralAccountLiquidatingError(account);

        UFixed18 totalMaintenance = product.maintenance(account);
        UFixed18 totalCollateral = collateral(account, product); >>>audit: cache balances[account]

        if (!totalMaintenance.gt(totalCollateral))
            revert CollateralCantLiquidate(totalMaintenance, totalCollateral);

        product.closeAll(account); >>>audit: operate on balances[account], can make it 0

        // claim fee
        UFixed18 liquidationFee = controller().liquidationFee();
        // If maintenance is less than minCollateral, use minCollateral for fee amount
        UFixed18 collateralForFee = UFixed18Lib.max(totalMaintenance, controller().minCollateral());
        UFixed18 fee = UFixed18Lib.min(totalCollateral, collateralForFee.mul(liquidationFee)); >>>audit: fee calculated from previous cache of balances[account]

        _products[product].debitAccount(account, fee); >>>audit: debit could fail.
        token.push(msg.sender, fee);

        emit Liquidation(account, product, msg.sender, fee);
    }
```

OptimisticLedger
```solidity
    function debitAccount(OptimisticLedger storage self, address account, UFixed18 amount) internal {
        self.balances[account] = self.balances[account].sub(amount);
        self.total = self.total.sub(amount);
    }

    /**
     * @notice Credits `account` with `amount` collateral
     * @dev Funds come from inside the product, not totals are updated
     *      Shortfall is created if more funds are debited from an account than exist
     * @param self The struct to operate on
     * @param account Account to credit collateral to
     * @param amount Amount of collateral to credit
     * @return newShortfall Any new shortfall incurred during this settlement
     */
    function settleAccount(OptimisticLedger storage self, address account, Fixed18 amount)
    internal returns (UFixed18 newShortfall) {
        Fixed18 newBalance = Fixed18Lib.from(self.balances[account]).add(amount);

        if (newBalance.sign() == -1) {
            newShortfall = newBalance.abs();
            newBalance = Fixed18Lib.ZERO;
        }

        self.balances[account] = newBalance.abs();
        self.shortfall = self.shortfall.add(newShortfall);
    }
```

closeAll
```solidity
    function closeAll(address account) external onlyCollateral notClosed settleForAccount(account) {
        AccountPosition storage accountPosition = _positions[account];
        Position memory p = accountPosition.position.next(_positions[account].pre);

        // Close all positions
        _closeMake(account, p.maker);
        _closeTake(account, p.taker);

        // Mark liquidation to lock position
        accountPosition.liquidation = true;
    }
```
_closeMake 
```solidity
    function _closeMake(address account, UFixed18 amount) private {
        IOracleProvider.OracleVersion memory latestOracleVersion = atVersion(latestVersion());

        _positions[account].pre.closeMake(latestOracleVersion.version, amount);
        _position.pre.closeMake(latestOracleVersion.version, amount);

        UFixed18 positionFee = amount.mul(latestOracleVersion.price.abs()).mul(makerFee());
        if (!positionFee.isZero()) {
            controller().collateral().settleAccount(account, Fixed18Lib.from(-1, positionFee));
            emit PositionFeeCharged(account, latestOracleVersion.version, positionFee);
        }

        emit PositionFeeCharged(account, latestOracleVersion.version, positionFee);
        emit MakeClosed(account, latestOracleVersion.version, amount);
    }
```
omitting `_closeTake` since it just call `settleAccount` like `_closeMake`

## Impact
liquidate would revert when the liquidation fee is reduced from a balance of 0
## Code Snippet
https://github.com/sherlock-audit/2023-05-perennial/blob/main/perennial-mono/packages/perennial/contracts/collateral/Collateral.sol#L108-L135

## Tool used

Manual Review

## Recommendation
fetch the latest totalCollateral of the user after `closeAll`. use this latest totalCollateral to calculate liquidation fee, consider adding the liquidation fee on shortfall if this is supposed to keep the liquidate call competitive.