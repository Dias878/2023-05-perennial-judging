dacian

medium

# Unnecessary precision loss to targetCollateral in BalancedVault._rebalanceCollateral()

## Summary
There is an unnecessary precision loss to targetCollateral calculation in BalancedVault._rebalanceCollateral().

## Vulnerability Detail
Consider [BalancedVault._rebalanceCollateral() L442-444](https://github.com/sherlock-audit/2023-05-perennial/blob/main/perennial-mono/packages/perennial-vaults/contracts/balanced/BalancedVault.sol#L442-L444):
```solidity
UFixed18 targetCollateral = _assets().sub(claimAmount).div(TWO);
if (targetCollateral.muldiv(minWeight, totalWeight).lt(controller.minCollateral()))
	targetCollateral = UFixed18Lib.ZERO;
```
``targetCollateral`` has division performed then gets multiplied resulting in precision loss due to division before multiplication.

## Impact
Can impact the correctness of the if statement check.

## Code Snippet
I've implemented a fuzz testing using Foundry to show the precision loss between the original and a correct version of the calculation:

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.20;

contract PerennialPrecisionLoss {
    // @audit simple re-implementation of Perennial BalancedVault._rebalanceCollateral()
    function originalCollateral_rebalanceCollateral(
        uint assets,
        uint claimAmount,
        uint minWeight,
        uint totalWeight) external pure returns (uint) {
        // let targetCollateral = assets.sub(claimAmount).div(TWO);
        // 
        // targetCollateral.muldiv(minWeight, totalWeight)
        // = (assets - claimAmount) / 2e18 * minWeight / totalWeight
        return (assets - claimAmount) / 2e18 * minWeight / totalWeight;
    }

    function simplifiedCollateral_rebalanceCollateral(
        uint assets,
        uint claimAmount,
        uint minWeight,
        uint totalWeight) external pure returns (uint) {
        // corrected to remove division before multiplication:
        // = (assets - claimAmount) * minWeight / (totalWeight*2e18)
        return (assets - claimAmount) * minWeight / (totalWeight * 2e18);
    }
}
```

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.20;

import {PerennialPrecisionLoss} from "../src/PerennialPrecisionLoss.sol";

import {console2} from "forge-std/console2.sol";
import {Test} from "forge-std/Test.sol";

contract PerennialPrecisionLossTest is Test {
    // real contract
    PerennialPrecisionLoss internal _underlying;

    function setUp() public {
        _underlying = new PerennialPrecisionLoss();
    }

    uint public originalOutput;
    uint public simplifiedOutput;

    uint public maxPrecisionLoss;
    uint public mpl_assets;
    uint public mpl_claimAmount;
    uint public mpl_minWeight;
    uint public mpl_totalWeight;

    function testFuzzCollateral_rebalanceCollateral(
        uint assets,
        uint claimAmount,
        uint minWeight,
        uint totalWeight) public {
        // constrain inputs to their respective precision range & valid input range
        assets      = bound(assets,      1e18, 1000000000e18);
        claimAmount = bound(claimAmount, 1e18, 1000000000e18);
        minWeight   = bound(minWeight,   1e18, 1000000000e18);
        totalWeight = bound(totalWeight, 1e18, 1000000000e18);

        vm.assume(minWeight<= totalWeight);
        vm.assume(assets >= claimAmount);
        
        // run both original & simplified functions
        originalOutput   
            = _underlying.originalCollateral_rebalanceCollateral(
                assets, claimAmount, minWeight, totalWeight);
        simplifiedOutput 
            = _underlying.simplifiedCollateral_rebalanceCollateral(
                assets, claimAmount, minWeight, totalWeight);

        // find the difference in precision loss
        uint precisionLoss = simplifiedOutput >= originalOutput 
                           ? simplifiedOutput - originalOutput
                           : originalOutput - simplifiedOutput;

        //
        // if this run produced greater precision loss than all previous,
        // or if the precision loss was the same AND originalOutput == 0
        // AND simplifiedOutput > 0, then save it & its inputs
        //
        // we are really interested in seeing if we can reach a state
        // where originalOutput == 0 && simplifiedOutput > 0 as this is
        // a more damaging form of precision loss
        //
        if(precisionLoss > 0) {
            if(precisionLoss > maxPrecisionLoss || 
                (precisionLoss == maxPrecisionLoss 
              && originalOutput == 0 && simplifiedOutput > 0)) {
                maxPrecisionLoss = precisionLoss;
                mpl_assets       = assets;
                mpl_claimAmount  = claimAmount;
                mpl_minWeight    = minWeight;
                mpl_totalWeight  = totalWeight;
                
                console2.log("originalOutput   : ", originalOutput);
                console2.log("simplifiedOutput : ", simplifiedOutput);
                console2.log("maxPrecisionLoss : ", maxPrecisionLoss);
                console2.log("mpl_assets       : ", mpl_assets);
                console2.log("mpl_claimAmount  : ", mpl_claimAmount);
                console2.log("mpl_minWeight    : ", mpl_minWeight);
                console2.log("mpl_totalWeight  : ", mpl_totalWeight);

                assert(false);
            }            
        }
    }
}
```

Run with: ``forge test --match-test testFuzzCollateral_rebalanceCollateral -vvvv``

Returns output eg:
```solidity
originalOutput   :  499999997
simplifiedOutput :  499999998
maxPrecisionLoss :  1
mpl_assets       :  999999999000000000000000005
mpl_claimAmount  :  1000000000000000006
mpl_minWeight    :  1000000000000000000
mpl_totalWeight  :  1000000000000000001
```

## Tool used
Manual Review

## Recommendation
Refactor to perform division last, eg: ``(assets - claimAmount) * minWeight / (totalWeight * 2e18);``
